plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'org.unbroken-dome.test-sets' version '1.4.5'
    id 'com.google.protobuf' version '0.8.5'
    id 'findbugs'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'corfu.logstorageunit.App'

dependencies {
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
}

sourceSets {
    main {
        proto {
            srcDir '../logstorageunitprotocol/src/main/proto'
        }
    }
}

tasks.withType(FindBugs) {
    ignoreFailures = true
    excludeFilter = file("$projectDir/findbugs-exclude.xml")
}

testSets {
    integrationTest
}

integrationTest {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = true
    }
    maxParallelForks = 1
}
